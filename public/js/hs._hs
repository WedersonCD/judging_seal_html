def getStars(starts)
    make a <div.content-rating-starts.flex-row/> called contentRatingStarts

    repeat in [1,2,3,4,5]
        set rate to it

        if rate <= starts then
            set src to '/images/star_fish.png'
        else
             set src to '/images/star_fish_grey.png'
        end
        
        make a <img/> called star
            set star's src to src
            set star's height to '21'
            set star's width to '21'

        make a <div.content-rating-start/> called contentRating
            put star into contentRating
            tell contentRating
                set @rate to rate
            end

            put contentRating at end of contentRatingStarts

    end
    return contentRatingStarts
end 

behavior containerTabChanging(containerTabClass, containerContentClassToHide,containerContentClassToShow)
    on click 
        remove .selected from containerTabClass
        add .selected

        add .display-none to containerContentClassToHide
        remove .display-none from containerContentClassToShow
        
    end
end

behavior validateEmail
    on focusout
        set email to my value
        set regexValidation to '^[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\.[a-zA-Z]{2,}$'
        call email.match(regexValidation) 
        if not result then
            add .invalid
        else
            remove .invalid
        end
    end
end